plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.5'
	id 'io.spring.dependency-management' version '1.1.3'
	id "com.github.node-gradle.node" version "4.0.0" // gradle에서 npm 사용
}

task appNpmInstall(type: NpmTask) {
  dependsOn "npmSetup"
  description = "Installs dependencies from package.json"
  workingDir = file("${projectDir}/src/main/resources/static");
  args = ["install"]
}

// 1번 - React build 설정
def reactAppDir = "${projectDir}/src/main/webapp"

processResources {
	// 2번 - task간의 의존성 정의
	// processResources task 실행되기 전에 copyReactFile task를 먼저 실행한다.
	dependsOn "copyReactFile" // task 수행전 task간 의존성 주입(내 프로세스 실행하기전 얘를 먼저 수행해 주세요 라는 의미)
}

task copyReactFile(type: Copy) { // 3번 - 의존성 주입에 의해 먼저 실행
	dependsOn "buildReact"
	from "$reactAppDir/build" // 리액트에서 잡고 있는 환경이
	into "$projectDir/src/main/resources/static/" // 여기로 복사되서 들어옴
}

// 4번 - 변경이 있을 시에만 수행하는 task
task buildReact(type: Exec) {
	dependsOn "installReact"
	workingDir "$reactAppDir"
	// $reactAppDir 디렉토리의 변경 사항을 감지하고, 변경이 있을 경우에만 task가 실행되도록 한다.
	inputs.dir "$reactAppDir"
	group = BasePlugin.BUILD_GROUP
	// os가 windows일시에 if문 수행
	if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
		commandLine "npm.cmd", "run-script", "build"
	} else {
		commandLine "npm", "run-script", "build"
	}
}

// 5번 - 실제로 수행되는 task
task installReact(type: Exec) {
	workingDir "$reactAppDir"
	inputs.dir "$reactAppDir"
	group = BasePlugin.BUILD_GROUP
	// os가 windows일시에 if문 수행
	if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
		commandLine "npm.cmd", "audit", "fix"
		commandLine "npm.cmd", "install"
	} else {
		commandLine "npm", "audit", "fix"
		commandLine "npm", "install"
	}
}


group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	
	// https://mvnrepository.com/artifact/io.awspring.cloud/spring-cloud-starter-aws
	implementation 'io.awspring.cloud:spring-cloud-starter-aws:2.4.4'
	implementation 'com.amazonaws:aws-java-sdk-s3:1.12.561'
}

tasks.named('test') {
	useJUnitPlatform()
}
